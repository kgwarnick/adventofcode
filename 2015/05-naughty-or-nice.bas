1000 REM Advent of Code 2015, Day 5, Doesn't He Have Intern-Elves For This?
1005 REM https://adventofcode.com/2015/day/5
1100 PRINT "--- Beispiel ---"
1110 DIM WOERTER$(1024)
1115 WOERTER$(1) = "aei"
1116 WOERTER$(2) = "xazegov"
1117 WOERTER$(3) = "aeiouaeiouaeiou"
1118 WOERTER$(4) = "abcdde"
1119 WOERTER$(5) = "aabbccdd"
1120 WOERTER$(6) = "ugknbfddgicrmopn"
1121 WOERTER$(7) = "aaa"
1122 WOERTER$(8) = "jchzalrnumimnmhp"
1124 WOERTER$(9) = "haegwjzuvuyypxyu"
1125 WOERTER$(10) = "dvszwmarrgswjxmb"
1130 ANZAHLWOERTER% = 10
1140 PRINT ANZAHLWOERTER%; "W”rter -> Nice? (Vokale, Doppelte, Verbotene)"
1150 FOR W = 1 TO ANZAHLWOERTER%
1155 WORT$ = WOERTER$(W)
1160 GOSUB 2500
1165 IF NICE% THEN NICE$ = "Nice" ELSE NICE$ = "Naughty"
1180 PRINT W, WORT$; " -> "; NICE$, ANZAHLVOKALE%; ANZAHLDOPPELBUCHSTABEN%; ANZAHLVERBOTENEKOMBIS%
1190 NEXT
1195 PRINT
1200 REM
1201 PRINT "--- Aufgabe 1: How many strings are nice? ---"
1210 DATEINAME$ = "05-in.txt" : GOSUB 3000
1215 PRINT "W”rter:"; ANZAHLWOERTER%
1220 NUMNICE% = 0 : NUMNAUGHTY% = 0
1230 PRINT ANZAHLWOERTER%; "W”rter -> Nice? (Vokale, Doppelte, Verbotene)"
1240 FOR W = 1 TO ANZAHLWOERTER%
1245 WORT$ = WOERTER$(W)
1250 GOSUB 2500
1255 IF NICE% THEN NICE$ = "Nice" ELSE NICE$ = "Naughty"
1260 IF (NICE%) THEN NUMNICE% = NUMNICE% + 1 ELSE NUMNAUGHTY% = NUMNAUGHTY% + 1
1270 PRINT W, WORT$; " -> "; NICE$, ANZAHLVOKALE%; ANZAHLDOPPELBUCHSTABEN%; ANZAHLVERBOTENEKOMBIS%
1280 NEXT
1290 PRINT "*** Ergebnis:  Nice ="; NUMNICE%; ",  Naughty ="; NUMNAUGHTY%
1295 PRINT
1900 REM
1901 PRINT "--- Kurztests ---"
1950 WORT$ = "abcdefghhijjkl"
1960 GOSUB 2000   ' Vokale z„hlen
1965 PRINT WORT$; " -> Anzahl Vokale:            "; ANZAHLVOKALE%
1970 GOSUB 2100   ' Doppel-Buchstaben z„hlen
1975 PRINT WORT$; " -> Anzahl Doppel-Buchstaben: "; ANZAHLDOPPELBUCHSTABEN%
1980 GOSUB 2200   ' Verbotene Buchstaben-Kombinationen z„hlen
1985 PRINT WORT$; " -> Anzahl verbotene Buchstaben-Kombinationen: "; ANZAHLVERBOTENEKOMBIS%
1990 END
2000 REM
2001 REM Vokale z„hlen
2002 REM Input:  Wort$
2003 REM Output: AnzahlVokale%
2004 REM
2010 ANZAHLVOKALE% = 0
2015 FOR I = 1 TO LEN(WORT$)
2020 IF MID$ (WORT$, I, 1) = "a" OR MID$ (WORT$, I, 1) = "e" OR MID$ (WORT$, I, 1) = "i" OR MID$ (WORT$, I, 1) = "o" OR MID$ (WORT$, I, 1) = "u" THEN ANZAHLVOKALE% = ANZAHLVOKALE% + 1
2050 NEXT
2090 RETURN
2100 REM
2101 REM Doppelbuchstaben z„hlen
2102 REM Input:  Wort$
2103 REM Output: AnzahlDoppelBuchstaben%
2104 REM
2110 ANZAHLDOPPELBUCHSTABEN% = 0
2115 FOR I = 1 TO LEN(WORT$) - 1
2120 IF MID$ (WORT$, I, 1) = MID$ (WORT$, I + 1, 1) THEN ANZAHLDOPPELBUCHSTABEN% = ANZAHLDOPPELBUCHSTABEN% + 1
2150 NEXT I
2190 RETURN
2200 REM
2201 REM Verbotenes Kombinationen z„hlen
2202 REM Input:  Wort$
2203 REM Output: AnzahlVerboteneKombis%
2204 REM
2210 ANZAHLVERBOTENEKOMBIS% = 0
2215 FOR I = 1 TO LEN(WORT$) - 1
2220 IF MID$ (WORT$, I, 2) = "ab" OR MID$ (WORT$, I, 2) = "cd" OR MID$ (WORT$, I, 2) = "pq" OR MID$ (WORT$, I, 2) = "xy" THEN ANZAHLVERBOTENEKOMBIS% = ANZAHLVERBOTENEKOMBIS + 1
2250 NEXT
2290 RETURN
2500 REM
2501 REM Auf "Nice String" testen
2502 REM Input:  Wort$
2503 REM Output: Nice% (bool)
2504 REM
2510 GOSUB 2000 : GOSUB 2100 : GOSUB 2200
2515 NICE% = (ANZAHLVOKALE% > 2) AND (ANZAHLDOPPELBUCHSTABEN% > 0) AND (ANZAHLVERBOTENEKOMBIS% <= 0)
2550 RETURN
3000 REM
3001 REM Eingabedatei lesen
3002 REM Input:  Dateiname$
3003 REM Output: Woerter$(1024), AnzahlWoerter%
3004 REM
3010 OPEN "I", #1, DATEINAME$
3015 ANZAHLWOERTER% = 0
3020 WHILE NOT EOF(1)
3025 LINE INPUT #1, S$
3030 ANZAHLWOERTER% = ANZAHLWOERTER% + 1
3035 WOERTER$(ANZAHLWOERTER%) = S$
3070 WEND
3085 CLOSE #1
3090 RETURN
